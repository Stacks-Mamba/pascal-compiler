<program> ::= PROGRAM ID PONTOVIRGULA <block> PONTO
<block> ::= <label declaration part> <constant definition part> <type definition part> <variable declaration part> <procedure and function declaration part> <statement part>
<label declaration part> ::= <empty> | LABEL <label> {VIRGULA <label>} PONTOVIRGULA
<label> ::= NUMINT
<constant definition part> ::= <empty> | CONST <constant definition> { PONTOVIRGULA <constant definition>} PONTOVIRGULA
<constant definition> ::= ID IGUAL <constant>
<constant> ::= <unsigned number> | <sign> <unsigned number> | <constant identifier> | <sign> <constant identifier> | STRING
<unsigned number> ::= NUMINT | NUMREAL
<sign> ::= + | -
<constant identifier> ::= ID
<type definition part> ::= <empty> | TYPE <type definition> {PONTOVIRGULA <type definition>} PONTOVIRGULA
<type definition> ::= ID IGUAL <type>
<type> ::= <simple type> | <structured type> | <pointer type>
<simple type> ::= <scalar type> | <subrange type> | <type identifier>
<scalar type> ::= ABREPAR ID{VIRGULA ID} FECHAPAR
<subrange type> ::= <constant> PONTOPONTO <constant>
<type identifier> ::= ID
<structured type> ::= <array type> | <record type> | <set type> | <file type>
<array type> ::= ARRAY [ <index type> { VIRGULA <index type>} ] OF <component type>
<index type> ::= <simple type>
<component type> ::= <type>
<record type> ::= RECORD <field list> END
<field list> ::= <fixed part> | <fixed part> PONTOVIRGULA <variant part> | <variant part>
<fixed part> ::= <record section> {PONTOVIRGULA <record section>}
<record section> ::= <field identifier> {VIRGULA <field identifier>} PONTOVIRGULA <type> | <empty>
<variant type> ::= CASE <tag field> <type identifier> OF <variant> { PONTOVIRGULA <variant>}
<tag field> ::= <field identifier> DOISPONTOS | <empty>
<variant> ::= <case label list> DOISPONTOS ABREPAR <field list> FECHAPAR | <empty>
<case label list> ::= <case label> {VIRGULA <case label>}
<case label> ::= <constant>
<set type> ::=SET OF <base type>
<base type> ::= <simple type>
<file type> ::= FILE OF <type>
<pointer type> ::= <type identifier>
<variable declaration part> ::= <empty> | VAR <variable declaration> {PONTOVIRGULA <variable declaration>} PONTOVIRGULA
<variable declaration> ::= ID {VIRGULA ID} DOISPONTOS <type>
<procedure and function declaration part> ::= {<procedure or function declaration> PONTOVIRGULA}
<procedure or function declaration> ::= <procedure declaration> | <function declaration>
<procedure declaration> ::= <procedure heading> <block>
<procedure heading> ::= PROCEDURE ID <procedure heading 1>
<procedure heading 1> ::= PONTOVIRGULA |ABREPAR <formal parameter section> {PONTOVIRGULA <formal parameter section>} FECHAPAR PONTOVIRGULA
<formal parameter section> ::= <parameter group> | VAR <parameter group> | FUNCTION <parameter group> | PROCEDURE ID { VIRGULA ID}
<parameter group> ::= ID {VIRGULA ID} DOISPONTOS <type identifier>
<function declaration> ::= <function heading> <block>
<function heading> ::= FUNCTION ID <function heading 1>
<function heading 1> ::= DOISPONTOS <result type> PONTOVIRGULA |ABREPAR <formal parameter section> {PONTOVIRGULA <formal parameter section>} FECHAPAR DOISPONTOS <result type> PONTOVIRGULA
<result type> ::= <type identifier>
<statement part> ::= <compound statement>
<statement> ::= <unlabelled statement> | <label> DOISPONTOS <unlabelled statement>
<unlabelled statement> ::= <simple statement> | <structured statement>
<simple statement> ::= <assignment statement> | <procedure statement> | <go to statement> | <empty statement>
<assignment statement> ::= <variable> DOISPONTOSIGUAL <expression> | <function identifier> DOISPONTOSIGUAL <expression>
<variable> ::= <entire variable> | <component variable> | <referenced variable>
<entire variable> ::= <variable identifier>
<variable identifier> ::= ID
<component variable> ::= <indexed variable> | <field designator> | <file buffer>
<indexed variable> ::= <array variable> ABRERET <expression> {VIRGULA <expression>} FECHARET
<array variable> ::= <variable>
<field designator> ::= <record variable> PONTO <field identifier>
<record variable> ::= <variable>
<field identifier> ::= ID
<file buffer> ::= <file variable>
<file variable> ::= <variable>
<referenced variable> ::= <pointer variable>
<pointer variable> ::= <variable>
<expression> ::= <simple expression> | <simple expression> <relational operator> <simple expression>
<relational operator> ::= IGUAL | DIFERENTE | MENOR | MENORIGUAL | MAIORIGUAL | MAIOR | IN
<simple expression> ::= <term> | <sign> <term>| <simple expression> <adding operator> <term>
<adding operator> ::= MAIS | MENOS | OR
<term> ::= <factor> | <term> <multiplying operator> <factor>
<multiplying operator> ::= VEZES | DIVISAO | DIV | MOD | AND
<factor> ::= <variable> | <unsigned constant> | ABREPAR <expression> FECHAPAR | <function designator> | <set> | NOT <factor>
<unsigned constant> ::= <unsigned number> | STRING | <constant identifier> | NIL
<function designator > ::= <function identifier> <function designator 1>
<function designator 1> ::= ABREPAR <actual parameter> {VIRGULA <actual parameter>} FECHAPAR | <empty>
<function identifier> ::= ID
<set> ::= ABRERET <element list> FECHARET
<element list> ::= <element> {VIRGULA <element> } | <empty>
<element> ::= <expression> <element 1>
<element 1> ::= PONTOPONTO <expression> | <empty>
<procedure statement> ::= <procedure identifier> <procedure statement 1>
<procedure statement 1> ::= ABREPAR <actual parameter> {VIRGULA <actual parameter> } FECHAPAR | <empty>
<procedure identifier> ::= ID
<actual parameter> ::= <expression> | <variable> | <procedure identifier> | <function identifier>
<go to statement> ::= GOTO <label>
<empty statement> ::= <empty>
<empty> ::=
<structured statement> ::= <compound statement> | <conditional statement> | <repetitive statement> | <with statement>
<compound statement> ::= BEGIN <statement> {PONTOVIRGULA <statement> } END PONTOVIRGULA
<conditional statement> ::= <if statement> | <case statement>
<if statement> ::= IF <expression> THEN <statement> <if statement 2>
<if statement 2> ::= ELSE <statement> | <empty>
<case statement> ::= CASE <expression> OF <case list element> {PONTOVIRGULA <case list element> } END
<case list element> ::= <case label list> PONTO <statement> | <empty>
<case label list> ::= <case label> {VIRGULA <case label>}
<repetitive statement> ::= <while statement> | <repeat statemant> | <for statement>
<while statement> ::= WHILE <expression> DO <statement>
<repeat statement> ::= REPEAT <statement> {PONTOVIRGULA <statement>} UNTIL <expression>
<for statement> ::= FOR <control variable> DOISPONTOSIGUAL <for list> DO <statement>
<control variable> ::= ID
<for list> ::= <initial value> TO <final value> | <initial value> DOWNTO <final value>
<initial value> ::= <expression>
<final value> ::= <expression>
<with statement> ::= WITH <record variable list> DO <statement>
<record variable list> ::= <record variable> {VIRGULA <record variable>}
